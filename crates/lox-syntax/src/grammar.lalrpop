// This grammar has been adapted from
// https://craftinginterpreters.com/appendix-i.html#syntax-grammar.
//
// TODO: This grammar does not handle field access (foo.bar).
// This is missing from ExprAssign and ExprCall (possibly others).

use crate::{ast, lexer};

use lalrpop_util::ParseError;

grammar<'err>(
    errors: &'err mut Vec<ParseError<usize, lexer::Token, lexer::Error>>
);

pub Program: ast::Program = <stmts:DeclS*> => ast::Program { <> };

// Declarations
DeclS = Spanned<Decl>;

Decl = {
    DeclClass,
    DeclFun,
    DeclVar,
    Stmt,
}

DeclClass: ast::Stmt =
    "class" <name:identifier> <base:("<" <identifier>)?> "{" <methods:Function*> "}" =>
        ast::Stmt::Class(ast::StmtClass { <> });

DeclFun: ast::Stmt = "fun" <function:Function> => ast::Stmt::Fun(Box::new(<>));

DeclVar: ast::Stmt = "var" <name:identifier> <value:("=" <ExprS>)?> ";" =>
    ast::Stmt::Var(ast::StmtVar { <> });

// Statements
// https://en.wikipedia.org/wiki/Dangling_else#Avoiding_the_conflict_in_LR_parsers
Stmt: ast::Stmt = {
    StmtOpen,
    StmtClosed,
    <e:!> => {
        errors.push(e.error);
        ast::Stmt::Error
    },
}

StmtOpen: ast::Stmt = {
    "if" "(" <cond:ExprS> ")" <then:Spanned<Stmt>> =>
        ast::Stmt::If(Box::new(ast::StmtIf { <>, else_: None })),
    "if" "(" <cond:ExprS> ")" <then:Spanned<StmtClosed>> "else" <else_:Spanned<StmtOpen>> =>
        ast::Stmt::If(Box::new(ast::StmtIf { cond, then, else_: Some(else_) })),
    "while" "(" <cond:ExprS> ")" <body:Spanned<StmtOpen>> =>
        ast::Stmt::While(Box::new(ast::StmtWhile { <> })),
    "for" "(" <init:ForInit> <cond:ForCond> <incr:ForIncr> ")" <body:Spanned<StmtOpen>> =>
        ast::Stmt::For(Box::new(ast::StmtFor { <> })),
}

StmtClosed: ast::Stmt = {
    "if" "(" <cond:ExprS> ")" <then:Spanned<StmtClosed>> "else" <else_:Spanned<StmtClosed>> =>
        ast::Stmt::If(Box::new(ast::StmtIf { cond, then, else_: Some(else_) })),
    "while" "(" <cond:ExprS> ")" <body:Spanned<StmtClosed>> =>
        ast::Stmt::While(Box::new(ast::StmtWhile { <> })),
    "for" "(" <init:ForInit> <cond:ForCond> <incr:ForIncr> ")" <body:Spanned<StmtClosed>> =>
        ast::Stmt::For(Box::new(ast::StmtFor { <> })),
    StmtSimple,
}

ForInit: Option<ast::StmtS> = {
    <Spanned<DeclVar>> => Some(<>),
    <Spanned<StmtExpr>> => Some(<>),
    ";" => None,
}

ForCond = <ExprS?> ";";

ForIncr = <ExprS?>;

StmtSimple = {
    StmtBlock,
    StmtExpr,
    StmtPrint,
    StmtReturn,
}

StmtBlock: ast::Stmt = StmtBlockInternal => ast::Stmt::Block(<>);

StmtBlockInternal: ast::StmtBlock = "{" <stmts:DeclS*> "}" =>
    ast::StmtBlock { <> };

StmtExpr: ast::Stmt = <value:ExprS> ";" =>
    ast::Stmt::Expr(ast::StmtExpr { <> });

StmtPrint: ast::Stmt = "print" <value:ExprS> ";" =>
    ast::Stmt::Print(ast::StmtPrint { <> });

StmtReturn: ast::Stmt = "return" <value:ExprS?> ";" =>
    ast::Stmt::Return(ast::StmtReturn { <> });

// Expressions
ExprS = Spanned<Expr>;

Expr = ExprAssign;

ExprAssign = {
    <name:identifier> "=" <value:ExprS> =>
        ast::Expr::Assign(Box::new(ast::ExprAssign { <> })),
    ExprLogicOr,
}

ExprLogicOr = ExprInfix<ExprLogicOr, OpLogicOr, ExprLogicAnd>;
OpLogicOr: ast::OpInfix = "or" => ast::OpInfix::LogicOr;

ExprLogicAnd = ExprInfix<ExprLogicAnd, OpLogicAnd, ExprEquality>;
OpLogicAnd: ast::OpInfix = "and" => ast::OpInfix::LogicAnd;

ExprEquality = ExprInfix<ExprEquality, OpEquality, ExprComparison>;
OpEquality: ast::OpInfix = {
    "==" => ast::OpInfix::Equal,
    "!=" => ast::OpInfix::NotEqual,
}

ExprComparison = ExprInfix<ExprComparison, OpComparison, ExprTerm>;
OpComparison: ast::OpInfix = {
    ">" => ast::OpInfix::Greater,
    ">=" => ast::OpInfix::GreaterEqual,
    "<" => ast::OpInfix::Less,
    "<=" => ast::OpInfix::LessEqual,
}

ExprTerm = ExprInfix<ExprTerm, OpTerm, ExprFactor>;
OpTerm: ast::OpInfix = {
    "+" => ast::OpInfix::Add,
    "-" => ast::OpInfix::Subtract,
}

ExprFactor = ExprInfix<ExprFactor, OpFactor, ExprPrefix>;
OpFactor: ast::OpInfix = {
    "*" => ast::OpInfix::Multiply,
    "/" => ast::OpInfix::Divide,
}

ExprPrefix: ast::Expr = {
    <op:OpPrefix> <rt:Spanned<ExprPrefix>> =>
        ast::Expr::Prefix(Box::new(ast::ExprPrefix { <> })),
    ExprCall,
}
OpPrefix: ast::OpPrefix = {
    "-" => ast::OpPrefix::Negate,
    "!" => ast::OpPrefix::Not,
}

ExprCall: ast::Expr = {
    <callee:Spanned<ExprCall>> "(" <args:Args> ")" => {
        ast::Expr::Call(Box::new(ast::ExprCall { callee, args }))
    },
    ExprPrimary,
}

ExprPrimary: ast::Expr = {
    // Literals
    "nil" => ast::Expr::Literal(ast::ExprLiteral::Nil),
    "false" => ast::Expr::Literal(ast::ExprLiteral::Bool(false)),
    "true" => ast::Expr::Literal(ast::ExprLiteral::Bool(true)),
    string => ast::Expr::Literal(ast::ExprLiteral::String(<>)),
    number => ast::Expr::Literal(ast::ExprLiteral::Number(<>)),

    // Variables
    <name:identifier> => ast::Expr::Variable(ast::ExprVariable { <> }),

    // Grouping
    "(" <Expr> ")",
}

// Utilities
Spanned<T>: ast::Spanned<T> = <l:@L> <t:T> <r:@R> => (t, l..r);

Function: ast::StmtFun =
    <name:identifier> "(" <params:Params> ")" <body:StmtBlockInternal> =>
        ast::StmtFun { <> };

Params: Vec<String> = {
    <first:identifier> <rest:("," <identifier>)*> => {
        let mut params = Vec::with_capacity(1 + rest.len());
        params.push(first);
        params.extend(rest);
        params
    },
    () => Vec::new(),
};

Args: Vec<ast::ExprS> = {
    <first:ExprS> <rest:("," <ExprS>)*> => {
        let mut args = Vec::with_capacity(1 + rest.len());
        args.push(first);
        args.extend(rest);
        args
    },
    () => Vec::new(),
}

ExprInfix<Lt, Op, Rt>: ast::Expr = {
    <lt:Spanned<Lt>> <op:Op> <rt:Spanned<Rt>> =>
        ast::Expr::Infix(Box::new(ast::ExprInfix { <> })),
    Rt,
}

extern {
    type Location = usize;
    type Error = lexer::Error;

    enum lexer::Token {
        // Single-character tokens.
        "(" => lexer::Token::LtParen,
        ")" => lexer::Token::RtParen,
        "{" => lexer::Token::LtBrace,
        "}" => lexer::Token::RtBrace,
        "," => lexer::Token::Comma,
        "." => lexer::Token::Dot,
        "-" => lexer::Token::Minus,
        "+" => lexer::Token::Plus,
        ";" => lexer::Token::Semicolon,
        "/" => lexer::Token::Slash,
        "*" => lexer::Token::Asterisk,

        // One or two character tokens.
        "!" => lexer::Token::Bang,
        "!=" => lexer::Token::BangEqual,
        "=" => lexer::Token::Equal,
        "==" => lexer::Token::EqualEqual,
        ">" => lexer::Token::Greater,
        ">=" => lexer::Token::GreaterEqual,
        "<" => lexer::Token::Less,
        "<=" => lexer::Token::LessEqual,

        // Literals.
        identifier => lexer::Token::Identifier(<String>),
        string => lexer::Token::String(<String>),
        number => lexer::Token::Number(<f64>),

        // Keywords.
        "and" => lexer::Token::And,
        "class" => lexer::Token::Class,
        "else" => lexer::Token::Else,
        "false" => lexer::Token::False,
        "for" => lexer::Token::For,
        "fun" => lexer::Token::Fun,
        "if" => lexer::Token::If,
        "nil" => lexer::Token::Nil,
        "or" => lexer::Token::Or,
        "print" => lexer::Token::Print,
        "return" => lexer::Token::Return,
        "super" => lexer::Token::Super,
        "this" => lexer::Token::This,
        "true" => lexer::Token::True,
        "var" => lexer::Token::Var,
        "while" => lexer::Token::While,
    }
}
