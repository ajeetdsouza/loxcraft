use crate::{ast, lexer};

use lalrpop_util::ParseError;

grammar<'err>(errors: &'err mut Vec<ParseError<usize, lexer::Token, lexer::Error>>);

pub Program: ast::Program = <stmts:Decl*> => ast::Program { <> };

Decl = {
    DeclFunS,
    Stmt,
}

DeclFunS: ast::StmtS = <l:@L> <fun:DeclFun> <r:@R> => (fun, l..r);

DeclFun: ast::Stmt =
    "fun" <name:identifier> "(" <params:Params> ")" <body:StmtBlockInternal> =>
        ast::Stmt::Fun(Box::new(ast::StmtFun { <> }));

Params: Vec<String> = {
    <first:identifier> <rest:("," <identifier>)*> => {
        let mut params = Vec::with_capacity(1 + rest.len());
        params.push(first);
        params.extend(rest);
        params
    },
    () => Vec::new(),
};

// https://en.wikipedia.org/wiki/Dangling_else#Avoiding_the_conflict_in_LR_parsers
Stmt: ast::StmtS = {
    StmtOpenS,
    StmtClosedS,
    <l:@L> <e:!> <r:@R> => {
        errors.push(e.error);
        (ast::Stmt::Error, l..r)
    },
}

StmtOpenS: ast::StmtS = <l:@L> <stmt:StmtOpen> <r:@R> => (stmt, l..r);

StmtOpen: ast::Stmt = {
    "if" "(" <cond:ExprS> ")" <then:Stmt> =>
        ast::Stmt::If(Box::new(ast::StmtIf { <>, else_: None })),
    "if" "(" <cond:ExprS> ")" <then:StmtClosedS> "else" <else_:StmtOpenS> =>
        ast::Stmt::If(Box::new(ast::StmtIf { cond, then, else_: Some(else_) })),
    "while" "(" <cond:ExprS> ")" <body:StmtOpenS> =>
        ast::Stmt::While(Box::new(ast::StmtWhile { <> })),
    "for" "(" <init:ForInit> <cond:ForCond> <incr:ForIncr> ")" <body:StmtOpenS> =>
        ast::Stmt::For(Box::new(ast::StmtFor { <> })),
}

StmtClosedS: ast::StmtS = <l:@L> <stmt:StmtClosed> <r:@R> => (stmt, l..r);

StmtClosed: ast::Stmt = {
    StmtSimple,
    "if" "(" <cond:ExprS> ")" <then:StmtClosedS> "else" <else_:StmtClosedS> =>
        ast::Stmt::If(Box::new(ast::StmtIf { cond, then, else_: Some(else_) })),
    "while" "(" <cond:ExprS> ")" <body:StmtClosedS> =>
        ast::Stmt::While(Box::new(ast::StmtWhile { <> })),
    "for" "(" <init:ForInit> <cond:ForCond> <incr:ForIncr> ")" <body:StmtClosedS> =>
        ast::Stmt::For(Box::new(ast::StmtFor { <> })),
}

ForInit: Option<ast::StmtS> = {
    <l:@L> <stmt:StmtVar> <r:@R> => Some((stmt, l..r)),
    <l:@L> <stmt:StmtExpr> <r:@R> => Some((stmt, l..r)),
    ";" => None,
}

ForCond: Option<ast::ExprS> = {
    <ExprS> ";" => Some(<>),
    ";" => None,
}

ForIncr: Option<ast::ExprS> = {
    <ExprS> => Some(<>),
    () => None
};

StmtSimple = {
    StmtBlock,
    StmtExpr,
    StmtPrint,
    StmtReturn,
    StmtVar,
}

StmtBlock: ast::Stmt = StmtBlockInternal => ast::Stmt::Block(<>);

StmtBlockInternal: ast::StmtBlock = "{" <stmts:Decl*> "}" =>
    ast::StmtBlock { <> };

StmtExpr: ast::Stmt =
    <value:ExprS> ";" => ast::Stmt::Expr(ast::StmtExpr { <> });

StmtPrint: ast::Stmt =
    "print" <value:ExprS> ";" => ast::Stmt::Print(ast::StmtPrint { <> });

StmtReturn: ast::Stmt =
    "return" <value:ExprS?> ";" => ast::Stmt::Return(ast::StmtReturn { <> });

StmtVar: ast::Stmt = {
    "var" <name:identifier> "=" <value:ExprS> ";" =>
        ast::Stmt::Var(ast::StmtVar { name, value: Some(value) }),
    "var" <name:identifier> ";" =>
        ast::Stmt::Var(ast::StmtVar { name, value: None }),
};

ExprS: ast::ExprS = <l:@L> <expr:Expr> <r:@R> => (expr, l..r);

Expr = ExprAssign;

ExprAssign = {
    <name:identifier> "=" <value:ExprS> =>
        ast::Expr::Assign(Box::new(ast::ExprAssign { <> })),
    ExprLogicOr,
}

ExprLogicOr = ExprInfix<ExprLogicOr, OpLogicOr, ExprLogicAnd>;
OpLogicOr: ast::OpInfix = "or" => ast::OpInfix::LogicOr;

ExprLogicAnd = ExprInfix<ExprLogicAnd, OpLogicAnd, ExprEquality>;
OpLogicAnd: ast::OpInfix = "and" => ast::OpInfix::LogicAnd;

ExprEquality = ExprInfix<ExprEquality, OpEquality, ExprComparison>;
OpEquality: ast::OpInfix = {
    "==" => ast::OpInfix::Equal,
    "!=" => ast::OpInfix::NotEqual,
}

ExprComparison = ExprInfix<ExprComparison, OpComparison, ExprTerm>;
OpComparison: ast::OpInfix = {
    ">" => ast::OpInfix::Greater,
    ">=" => ast::OpInfix::GreaterEqual,
    "<" => ast::OpInfix::Less,
    "<=" => ast::OpInfix::LessEqual,
}

ExprTerm = ExprInfix<ExprTerm, OpTerm, ExprFactor>;
OpTerm: ast::OpInfix = {
    "+" => ast::OpInfix::Add,
    "-" => ast::OpInfix::Subtract,
}

ExprFactor = ExprInfix<ExprFactor, OpFactor, ExprPrefix>;
OpFactor: ast::OpInfix = {
    "*" => ast::OpInfix::Multiply,
    "/" => ast::OpInfix::Divide,
}

ExprPrefix: ast::Expr = {
    <op:OpPrefix> <rt:Spanned<ExprPrefix>> =>
        ast::Expr::Prefix(Box::new(ast::ExprPrefix { <> })),
    ExprCall,
}
OpPrefix: ast::OpPrefix = {
    "-" => ast::OpPrefix::Negate,
    "!" => ast::OpPrefix::Not,
}

ExprCall: ast::Expr = {
    <callee:Spanned<ExprCall>> "(" <args:Args> ")" => {
        ast::Expr::Call(Box::new(ast::ExprCall { callee, args }))
    },
    ExprPrimary,
};

Args: Vec<ast::ExprS> = {
    <first:ExprS> <rest:("," <ExprS>)*> => {
        let mut args = Vec::with_capacity(1 + rest.len());
        args.push(first);
        args.extend(rest);
        args
    },
    () => Vec::new(),
}

ExprPrimary: ast::Expr = {
    <ExprLiteral> => ast::Expr::Literal(<>),
    <ExprVariable> => ast::Expr::Variable(<>),
    "(" <Expr> ")",
}

ExprLiteral: ast::ExprLiteral = {
    "nil" => ast::ExprLiteral::Nil,
    "false" => ast::ExprLiteral::Bool(false),
    "true" => ast::ExprLiteral::Bool(true),
    string => ast::ExprLiteral::String(<>),
    number => ast::ExprLiteral::Number(<>),
}

ExprVariable: ast::ExprVariable =
    <name:identifier> => ast::ExprVariable { <> };

ExprInfix<Lt, Op, Rt>: ast::Expr = {
    <lt:Spanned<Lt>> <op:Op> <rt:Spanned<Rt>> =>
        ast::Expr::Infix(Box::new(ast::ExprInfix { <> })),
    Rt,
}

Spanned<T>: ast::Spanned<T> = <l:@L> <t:T> <r:@R> => (t, l..r);

extern {
    type Location = usize;
    type Error = lexer::Error;

    enum lexer::Token {
        // Single-character tokens.
        "(" => lexer::Token::LtParen,
        ")" => lexer::Token::RtParen,
        "{" => lexer::Token::LtBrace,
        "}" => lexer::Token::RtBrace,
        "," => lexer::Token::Comma,
        "." => lexer::Token::Dot,
        "-" => lexer::Token::Minus,
        "+" => lexer::Token::Plus,
        ";" => lexer::Token::Semicolon,
        "/" => lexer::Token::Slash,
        "*" => lexer::Token::Asterisk,

        // One or two character tokens.
        "!" => lexer::Token::Bang,
        "!=" => lexer::Token::BangEqual,
        "=" => lexer::Token::Equal,
        "==" => lexer::Token::EqualEqual,
        ">" => lexer::Token::Greater,
        ">=" => lexer::Token::GreaterEqual,
        "<" => lexer::Token::Less,
        "<=" => lexer::Token::LessEqual,

        // Literals.
        identifier => lexer::Token::Identifier(<String>),
        string => lexer::Token::String(<String>),
        number => lexer::Token::Number(<f64>),

        // Keywords.
        "and" => lexer::Token::And,
        "class" => lexer::Token::Class,
        "else" => lexer::Token::Else,
        "false" => lexer::Token::False,
        "for" => lexer::Token::For,
        "fun" => lexer::Token::Fun,
        "if" => lexer::Token::If,
        "nil" => lexer::Token::Nil,
        "or" => lexer::Token::Or,
        "print" => lexer::Token::Print,
        "return" => lexer::Token::Return,
        "super" => lexer::Token::Super,
        "this" => lexer::Token::This,
        "true" => lexer::Token::True,
        "var" => lexer::Token::Var,
        "while" => lexer::Token::While,
    }
}
